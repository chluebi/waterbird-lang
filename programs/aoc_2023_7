map(f, list) {
    new = [];
    for e in list {
        new.push(f(e));
    }
    new
}

filter(p, list) {
    new = [];
    for e in list {
        if p(e) {
            new.push(e);
        }
    }
    new
}

any(p, list) {
    new = [];
    for e in list {
        if p(e) {
            return true;
        }
    }
    return false;
}

fold(step, start, list) {
    if list == [] {
        return start;
    } else {
        return fold(step, step(list[0], start), list[1:]);
    }
}

sum(list) {
    s = 0;
    for x in list {
        s += x;
    }
    s
}

concat(list) {
    s = [];
    for x in list {
        s += x;
    }
    s
}


surroundings_horizontal(inp, y, x, length=1) {
    ret = [];

    dx = -1;
    for dy in range(-1, 2) {
        if y+dy >= 0 && y+dy < len(inp) && x+dx >= 0 && x+dx < len(inp[y+dy]) {
            ret.push(((y+dy), (x+dx)));
        }
    }

    dx = -1;
    for dy in [-1, 1] {
        for dx in range(-1, length) {
            if y+dy >= 0 && y+dy < len(inp) && x+dx >= 0 && x+dx < len(inp[y+dy]) {
                ret.push(((y+dy), (x+dx)));
            }
        }
    }

    dx = length;
    for dy in range(-1, 2) {
        if y+dy >= 0 && y+dy < len(inp) && x+dx >= 0 && x+dx < len(inp[y+dy]) {
            ret.push(((y+dy), (x+dx)));
        }
    }

    return ret;
}


enumerate(list) {
    new_list = [];
    for i in range(len(list)) {
        new_list.push((i, list[i]));
    }
    new_list
}

min(*vals) {
    smallest = vals[0];
    for v in vals[1:] {
        if v < smallest {
            smallest = v;
        }
    }
    smallest
}

max(*vals) {
    largest = vals[0];
    for v in vals[1:] {
        if v > largest {
            largest = v;
        }
    }
    largest
}

binary_search(x, min, max, comp=|x,y|{x-y}, key=|x|{x}) {
    if comp(key(min), x) == 0 {
        return min;
    }

    guess = min + (max - min) / 2;

    
    while true {
        diff = comp(key(guess), x);

        if diff == 0 {
            return guess;
        } else if diff < 0 {
            min = guess + 1;
            if min > max {
                return guess;
            }
        } else {
            max = guess - 1;
            if max < min {
                return guess-1;
            }
        }
        guess = min + (max - min) / 2;
    }
}

binary_search_list(l, x, min, max, comp=|x,y|{x-y}) {
    binary_search(x, min, max, comp=comp, key=|x|{l[x]})
}

sort(l, key=|x|{x}) {

    for i in range(1, len(l)) {
        current_value = l[i];
        current_key = key(current_value);
        j = i - 1;

        min = 0;
        max = j;

        index = binary_search_list(l, current_key, min, max, comp=|x,y|{key(x)-y}) + 1;

        while j >= index {
            l[j+1] = l[j];
            j = j - 1;
        }

        l[j+1] = current_value;
    }
    
    return l;
}



comp_sort(l, key=|x,y|{x-y}) {

    for i in range(1, len(l)) {
        current_value = l[i];
        j = i - 1;

        min = 0;
        max = j;

        index = binary_search_list(l, current_value, min, max, comp=|x,y|{key(x,y)}) + 1;

        while j >= index {
            l[j+1] = l[j];
            j = j - 1;
        }

        l[j+1] = current_value;
    }

    for i in range(1, len(l)) {
        assert(key(l[i-1], l[i]));
    }
    
    return l;
}


pairs(l) {
    ret = [(l[i], l[i+1]) for i in range(0, len(l), 2)];
    ret
}

zip(l1, l2) {
    assert(len(l1) == len(l2));
    [(l1[i], l2[i]) for i in range(len(l1))]
}

sqrt(x) {
    if x == 0 {
        return 0;
    }
    if x == 1 {
        return 1;
    }

    min = 0;
    max = x;
    guess = min + (max - min) / 2;

    while true {
        if guess == x / guess {
            return guess;
        } else if guess < x / guess {
            min = guess + 1;
            if min > max {
                return guess;
            }
        } else {
            max = guess - 1;
            if max < min {
                return guess-1;
            }
        }
        guess = min + (max - min) / 2;
    }
}

sqrt_ceil(x) {
    sq = sqrt(x);
    if sq * sq < x {
        return sq + 1;
    }
    sq
}

join(l, sep="") {
    s = "";
    for i in range(len(l)) {
        s += l[i];
        if i < len(l) - 1 {
            s += sep;
        }
    }
    s
}

compare_hands(h1, h2, h1_comparators, h2_comparators) {

    for h1_comp, h2_comp in zip(h1_comparators, h2_comparators) {
        if !(h1_comp) && !(h2_comp) {
            continue;
        }

        if h1_comp && h2_comp {
            for c1, c2 in zip(h1, h2) {
                if c1 == c2 {
                    continue;
                }
                if c1 > c2 {
                    return 1;
                } else {
                    return -1;
                }
            }
        }

        if h1_comp {
            return 1;
        } else {
            return -1;
        }
    }

    return 0;
}


compare_hands2(h1, h2, h1_comparators, h2_comparators) {

    print(h1, h2, h1_comparators, h2_comparators);

    for h1_comp, h2_comp in zip(h1_comparators, h2_comparators) {
        print("iteration", h1_comp, h2_comp);
        if !(h1_comp) && !(h2_comp) {
            continue;
        }

        if h1_comp && h2_comp {
            print("comparisons", zip(h1, h2));
            for c1, c2 in zip(h1, h2) {
                if c1 == c2 {
                    continue;
                }
                if c1 > c2 {
                    return 1;
                } else {
                    return -1;
                }
            }
        }

        if h1_comp {
            return 1;
        } else {
            return -1;
        }
    }

    return 0;
}

part1(hands) {
    is_five = |x|{
        5 in x.values()
    };
    is_four = |x|{
        4 in x.values()
    };
    is_full = |x|{
        l = x.values();
        l == [2, 3] || l == [3, 2]
    };
    is_three = |x|{
        3 in x.values()
    };
    is_two_pair = |x|{
        l = x.values();
        l == [1, 2, 2] || l == [2, 1, 2] || l == [2, 2, 1]
    };
    is_two = |x|{
        2 in x.values()
    };
    is_high = |x| {true};

    comparators = [is_five, is_four, is_full, is_three, is_two_pair, is_two, is_high];

    hands = comp_sort([([c(count(h)) for c in comparators], bid, h) for h, bid in hands], key=|x,y|{compare_hands(x[2], y[2], x[0], y[0])});
    s = 0;

    for i, (h, bid, h2) in enumerate(hands) {
        s += (i+1) * int(bid);
    }

    s
} 



perms(h, cards) {
    index = 0;
    all = [[]];

    for card in h {
        if card == 0 {
            all_new = [];
            for l in all {
                for new_card in cards {
                    new_l = l.clone();
                    new_l.push(new_card);
                    all_new.push(new_l);
                }
            }
            all = all_new;
        } else {
            for l in all {
                l.push(card);
            }
        }
    }

    all
}

part2(hands, cards) {
    is_five = |x|{
        5 in x.values()
    };
    is_four = |x|{
        4 in x.values()
    };
    is_full = |x|{
        l = x.values();
        2 in l && 3 in l
    };
    is_three = |x|{
        3 in x.values()
    };
    is_two_pair = |x|{
        l = x.values();
        count = 0;
        for elt in l {
            if elt == 2 {
                count += 1;
                if count == 2 {
                    return true;
                }
            }
        }
        false
    };
    is_two = |x|{
        2 in x.values()
    };
    is_high = |x| {true};

    comparators = [is_five, is_four, is_full, is_three, is_two_pair, is_two, is_high];

    new_hands = [];

    for h, bid in hands {
        if 0 in h.count() && h.count()[0] >= 4 {
            new_hands.push(([true for c in comparators], bid, h));
        } else {
            all_perms = [count(perm) for perm in perms(h, cards)];
            new_hands.push(([any_true(
                #{
                    ret = [c(perm) for perm in all_perms];
                    ret
                }) for c in comparators], bid, h));

            dealloc(all_perms);
        }
    }

    new_hands = comp_sort(
        new_hands
        , key=|x,y|{compare_hands(x[2], y[2], x[0], y[0])});
    s = 0;

    for i, (h, bid, h2) in enumerate(new_hands) {
        s += (i+1) * int(bid);
    }

    s
} 


main() {
    inp = read("test_data/aoc_2023_7");
    if inp[-1] == "\n" {
        inp = inp[:-1];
    }
    inp = inp.split("\n");

    l = (["A", "K", "Q", "J", "T"] + map(str, [9, 8, 7, 6, 5, 4, 3, 2]))[::-1];

    hands_part1 = [];
    for line in inp {
        hand, bid = line.split();
        hand = [l.index(c) for c in hand];
        hands_part1.push((hand, bid));
    }

    l2 = (["A", "K", "Q", "T"] + map(str, [9, 8, 7, 6, 5, 4, 3, 2]) + ["J"])[::-1];

    hands_part2 = [];
    for line in inp {
        hand, bid = line.split();
        hand = [l2.index(c) for c in hand];
        hands_part2.push((hand, bid));
    }

    (part1(hands_part1), part2(hands_part2, [l2.index(c) for c in l2[1:]]))
} 